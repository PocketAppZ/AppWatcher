import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.0'
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://jitpack.io" }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

apply plugin: 'com.android.application'

configurations.all {
    resolutionStrategy.cacheDynamicVersionsFor 10, 'minutes'
}

dependencies {
    compile files('libs/protobuf-java-2.4.1.jar')
    compile files('libs/keyczar-0.71g-090613.jar')

    compile 'com.github.anod:android-utils-lib:1.0.1'

    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:recyclerview-v7:24.1.1'
    compile 'com.android.support:design:24.1.1'
    compile 'com.android.support:cardview-v7:24.1.1'
    compile 'com.android.support:palette-v7:24.1.1'

    compile 'com.google.android.gms:play-services-identity:9.2.1'
    compile 'com.google.android.gms:play-services-drive:9.2.1'
    compile 'com.google.android.gms:play-services-gcm:9.2.1'

    compile 'com.android.volley:volley:1.0.0'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.okhttp3:okhttp:3.3.1'
    compile 'com.squareup.picasso:picasso:2.6.0-SNAPSHOT'

    compile 'net.hockeyapp.android:HockeySDK:4.1.0-beta.2'

    // Find memory leaks tool
    debugCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
}

def buildTime() {
    def df = new SimpleDateFormat("yyyyMMddHm")
    return df.format(new Date())
}
android {
    compileSdkVersion 24
    buildToolsVersion '24.0.0'

    defaultConfig {
        applicationId 'com.anod.appwatcher'
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 32
        versionName '1.8.3'

        manifestPlaceholders = [HOCKEYAPP_APP_ID: APPWATCHER_HOCKEYAPP_ID]
        resValue "string", "content_authority", "com.anod.appwatcher"

        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }

        release {
            storeFile file(APPWATCHER_KEYSTORE_FILE)
            storePassword APPWATCHER_KEYSTORE_PASSWORD
            keyAlias APPWATCHER_KEY_ALIAS
            keyPassword APPWATCHER_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix ".debug"
            versionNameSuffix "-" + buildTime()
            proguardFile 'proguard-project.txt'
            proguardFile getDefaultProguardFile('proguard-android.txt')

            resValue "string", "content_authority", "com.anod.appwatcher.debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFile 'proguard-project.txt'
            proguardFile getDefaultProguardFile('proguard-android.txt')
            signingConfig signingConfigs.release
        }
    }


    lintOptions {
        disable 'InvalidPackage'
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}
